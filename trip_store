ruleset trip_store {
  meta {
    shares __testing, get_trips, get_long_trips, get_short_trips 
    provides get_trips, get_long_trips, get_short_trips
  }
  global {
    __testing = { "queries": [ { "name": "__testing" } ],
                  "events": [ {"domain": "car", "type": "trip_reset"}] }

    get_trips = function(){
      ent:trips
    }
    get_long_trips = function(){
      ent:long_trips
    }
    get_short_trips = function(){
      ent:trips.difference(ent:long_trips)
    }
  }

  rule collect_trips {
    select when explicit trip_processed
    pre{
      temp = {"mileage": event:attr("mileage"), "time": event:attr("time") }
    }
    fired{
      ent:trips:= ent:trips.defaultsTo([]).union([temp])
    }
  }

  rule collect_long_trips {
    select when explicit found_long_trip
    pre{
      temp = {"mileage": event:attr("mileage"), "time": event:attr("time") }
    }
    fired{
      ent:long_trips:= ent:long_trips.defaultsTo([]).union([temp])
    }
  }

  rule clear_trips {
    select when car trip_reset
    always{
      ent:trips:=[];
      ent:long_trips:=[]
    }
  }
}
